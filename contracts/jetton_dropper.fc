#include "imports/stdlib.fc";
#include "op_codes.fc";

;; storage variables
global int storage::merkle_root;
global int storage::merkle_depth;
global slice storage::owner;
global slice storage::data_tree_contract_addr;

global int ctx_id;

;; load_data populates storage variables using stored data
() load_data() impure {
    var ds = get_data().begin_parse();

    storage::merkle_root = ds~load_uint(256);
    storage::merkle_depth = ds~load_uint(8);
    storage::owner = ds~load_msg_addr();
    storage::data_tree_contract_addr = ds~load_msg_addr();
    ctx_id = ds~load_uint(32);
    ds.end_parse();
}

;; save_data stores storage variables as a cell into persistent storage
() save_data() impure {
    set_data(
        begin_cell()
            .store_uint(storage::merkle_root, 256)
            .store_uint(storage::merkle_depth, 8)
            .store_slice(storage::owner)
            .store_slice(storage::data_tree_contract_addr)
            .store_uint(ctx_id, 32)
            .end_cell()
    );
}

int hash_nodes(int a, int b) impure {
    return begin_cell().store_uint(a, 256).store_uint(b, 256).end_cell().cell_hash();
}

int get_node(cell p, int i) impure inline {
    ;;32 in udict_get is how much bits per oane key in dict
    (slice v, int ok) = p.udict_get?(32, i);
    throw_unless(error::node_not_found, ok);
    return v.preload_uint(256);
}

int check_proof(int root, cell proof, int leaf, int leaf_index, int depth) impure {
    int i = 0;
    int cur = leaf;
    while (i < depth) {
        int is_right = (leaf_index >> i) & 1;
        if (is_right) {
            cur = hash_nodes(proof.get_node(i), cur);
        } else {
            cur = hash_nodes(cur, proof.get_node(i));
        }
        i += 1;
    }
    return cur == root;
}

() send_internal_claim_message(int key, slice claimer_addres) impure {
    cell msg = begin_cell()
        .store_uint(0x18, 6)
        .store_slice(storage::data_tree_contract_addr)
        .store_coins(0)
        .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1) ;; default message headers (see sending messages page)
        .store_uint(op::data_claim,32)
        .store_uint(key,32)
        .store_slice(claimer_addres)
        .end_cell();

    send_raw_message(msg, 64);
}

;; recv_internal is the main function of the contract and is called when it receives a message from other contracts
() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_empty?()) { ;; ignore all empty messages
        return ();
    }

    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);
    if (flags & 1) { ;; ignore all bounced messages
        return ();
    }

    slice sender_address = cs~load_msg_addr();
    load_data(); ;; here we populate the storage variables

    int op = in_msg_body~load_uint(32); ;; by convention, the first 32 bits of incoming message is the op

    if(op == op::setroot){
        throw_unless(error::not_owner, equal_slices(sender_address, storage::owner));

        storage::merkle_root = in_msg_body~load_uint(256);
        storage::merkle_depth = in_msg_body~load_uint(8);
        save_data();
        return();
    }

    if(op == op::set_data_tree){
        throw_unless(error::not_owner, equal_slices(sender_address, storage::owner));

        storage::data_tree_contract_addr = in_msg_body~load_msg_addr();

        save_data();
        return();
    }

    if(op == op::claim){
        ;;Cell with proof
        cell proof = in_msg_body~load_ref();
        ;;Leaf => is key
        int leaf = in_msg_body~load_uint(32);
        ;;Leaf_index => is key index
        int leaf_index = in_msg_body~load_int(32);

        throw_unless(error::bad_proof, check_proof(
            storage::merkle_root, proof, leaf, leaf_index, storage::merkle_depth
        ));
        send_internal_claim_message(leaf_index,sender_address);

        save_data();
        return ();
    }

    throw(0xffff); ;; if the message contains an op that is not known to this contract, we throw
}

int get_id() method_id {
    load_data();
    return ctx_id;
}

int get_merkle_root() method_id {
    load_data();
    return storage::merkle_root;
}

int get_merkle_depth() method_id {
    load_data();
    return storage::merkle_depth;
}

slice get_data_tree_contract_addr() method_id {
    load_data();
    return storage::data_tree_contract_addr;
}
;;get owner